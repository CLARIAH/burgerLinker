---
# ********************************************* #
#                                               #
#  -------------------------------------------  #
#  KNAW BurgerLinker Civil Registry Data Model  #
#  -------------------------------------------  #
#                                               #
#  This file specifies the data model used by   #
#  the input data to BurgerLinker. All          #
#  requested variable bindings must be          #
#  resolvable for BurgerLinker to work          #
#  properly.                                    #
#                                               #
#  The data model specified in this files is    #
#  based upon the Civil Registry Schema, and is #
#  backwards compatible with past versions of   #
#  BurgerLinker.                                #
#                                               #
#  Version: AUG 2025                            #
#                                               #
# ********************************************* #


# ============================================= #
#                                               #
# SPARQL Query Bodies                           #
#                                               #
# ============================================= #

# Return all classes in the data and the
# frequency of their occurrence.
SUMMARY: >
  SELECT ?type (COUNT (?s) AS ?instanceCount)
  WHERE {
    ?s a ?type .
  } GROUP BY ?type

# Return all statements in the graph, and, if it
# concerns information about a person, also
# return the identifiers and event date.
STATEMENTS: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>

  SELECT ?s ?p ?o ?sPersonID ?oPersonID ?eventDate
  WHERE {
    ?s ?p ?o .

    OPTIONAL { ?s civ:personID ?sPersonID } .
    OPTIONAL { ?o civ:personID ?oPersonID } .
    OPTIONAL { ?event ?q ?s;
                      civ:eventDate ?eventDate . }
  }

# Return all information pertaining to birth
# events, including information about the newborn
# and its parents.
BIRTHS: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:newborn ?newbornID ;
         civ:father ?fatherNewbornID ;
         civ:mother ?motherNewbornID ;
         civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  ?newbornID sdo:givenName ?givenNameNewborn ;
            sdo:familyName ?familyNameNewborn ;
            sdo:gender ?genderNewborn .
  OPTIONAL { ?newbornID civ:age ?ageNewborn }
  OPTIONAL { ?newbornID civ:personID ?idNewborn }
  ?fatherNewbornID sdo:givenName ?givenNameNewbornFather ;
                   sdo:familyName ?familyNameNewbornFather ;
                   sdo:gender ?genderNewbornFather .
  OPTIONAL { ?fatherNewbornID civ:age ?ageNewbornFather }
  OPTIONAL { ?fatherNewbornID civ:personID ?idNewbornFather }
  ?motherNewbornID sdo:givenName ?givenNameNewbornMother ;
                   sdo:familyName ?familyNameNewbornMother ;
                   sdo:gender ?genderNewbornMother .
  OPTIONAL { ?motherNewbornID civ:age ?ageNewbornMother }
  OPTIONAL { ?motherNewbornID civ:personID ?idNewbornMother }
  }

# Return all information pertaining to death
# events, including information about the
# deceased, its partner, and its parents.
DEATHS: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:deceased ?deceasedID ;
         civ:partner ?partnerID ;
         civ:father ?fatherDeceasedID ;
         civ:mother ?motherDeceasedID ;
         civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  ?deceasedID sdo:givenName ?givenNameDeceased ;
              sdo:familyName ?familyNameDeceased ;
              sdo:gender ?genderDeceased .
  OPTIONAL { ?deceasedID civ:age ?ageDeceased }
  OPTIONAL { ?deceasedID civ:personID ?idDeceased }
  ?partnerID sdo:givenName ?givenNamePartner ;
             sdo:familyName ?familyNamePartner ;
             sdo:gender ?genderPartner .
  OPTIONAL { ?partnerID civ:age ?agePartner }
  OPTIONAL { ?partnerID civ:personID ?idPartner }
  ?fatherDeceasedID sdo:givenName ?givenNameDeceasedFather ;
                    sdo:familyName ?familyNameDeceasedFather ;
                    sdo:gender ?genderDeceasedFather .
  OPTIONAL { ?fatherDeceasedID civ:age ?ageDeceasedFather }
  OPTIONAL { ?fatherDeceasedID civ:personID ?idDeceasedFather }
  ?motherDeceasedID sdo:givenName ?givenNameDeceasedMother ;
                    sdo:familyName ?familyNameDeceasedMother ;
                    sdo:gender ?genderDeceasedMother .
  OPTIONAL { ?motherDeceasedID civ:age ?ageDeceasedMother }
  OPTIONAL { ?motherDeceasedID civ:personID ?idDeceasedMother }
  }

# Return all information pertaining to marriage
# events, including information about the
# bride and groom, and of both their parents.
MARRIAGES: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:bride ?brideID ;
         civ:groom ?groomID ;
         civ:fatherBride ?fatherBrideID ;
         civ:motherBride ?motherBrideID ;
         civ:fatherGroom ?fatherGroomID ;
         civ:motherGroom ?motherGroomID ;
         civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  ?brideID sdo:givenName ?givenNameBride ;
           sdo:familyName ?familyNameBride ;
           sdo:gender ?genderBride .
  OPTIONAL { ?brideID civ:age ?ageBride }
  OPTIONAL { ?brideID civ:personID ?idBride }
  ?groomID sdo:givenName ?givenNameGroom ;
           sdo:familyName ?familyNameGroom ;
           sdo:gender ?genderGroom .
  OPTIONAL { ?groomID civ:age ?ageGroom }
  OPTIONAL { ?groomID civ:personID ?idGroom }
  ?fatherBrideID sdo:givenName ?givenNameBrideFather ;
                 sdo:familyName ?familyNameBrideFather ;
                 sdo:gender ?genderBrideFather .
  OPTIONAL { ?fatherBrideID civ:age ?ageBrideFather }
  OPTIONAL { ?fatherBrideID civ:personID ?idBrideFather }
  ?motherBrideID sdo:givenName ?givenNameBrideMother ;
                 sdo:familyName ?familyNameBrideMother ;
                 sdo:gender ?genderBrideMother .
  OPTIONAL { ?motherBrideID civ:age ?ageBrideMother }
  OPTIONAL { ?motherBrideID civ:personID ?idBrideMother }
  ?fatherGroomID sdo:givenName ?givenNameGroomFather ;
                 sdo:familyName ?familyNameGroomFather ;
                 sdo:gender ?genderGroomFather .
  OPTIONAL { ?fatherGroomID civ:age ?ageGroomFather }
  OPTIONAL { ?fatherGroomID civ:personID ?idGroomFather }
  ?motherGroomID sdo:givenName ?givenNameGroomMother ;
                 sdo:familyName ?familyNameGroomMother ;
                 sdo:gender ?genderGroomMother .
  OPTIONAL { ?motherGroomID civ:age ?ageGroomMother }
  OPTIONAL { ?motherGroomID civ:personID ?idGroomMother }
  }

...  # EOF
