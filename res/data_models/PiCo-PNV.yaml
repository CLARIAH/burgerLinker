---
# ********************************************* #
#                                               #
#  -------------------------------------------  #
#  CBG BurgerLinker Persons in Context Model    #
#  -------------------------------------------  #
#                                               #
#  This file specifies the data model used by   #
#  the input data to BurgerLinker. All classes  #
#  and properties in this file must be defined. #
#                                               #
#  The data model specified in this files is    #
#  based upon the Persons in Context schema [1] #
#                                               #
#  [1] https://personsincontext.org             #
#                                               #
#  Version: AUG 2025                            #
#                                               #
# ********************************************* #


# ============================================= #
#                                               #
# SPARQL Queries                                #
#                                               #
# ============================================= #

# Return all information pertaining to birth
# events, including information about the newborn
# and its parents.
BIRTHS: >
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    ?event sdo:additionalType picot:sourcetypes\/551 ;
           sdo:dateCreated ?eventDate ;
           sdo:identifier ?eventID .
    ?newbornID prov:hadPrimarySource ?event ;
               picom:hasRole picot:roles\/575 ;
               sdo:identifier ?idNewborn .
    OPTIONAL { ?newbornID sdo:gender ?genderNewborn }
    OPTIONAL { ?newbornID picom:hasAge ?ageNewborn }
    OPTIONAL { ?newbornID sdo:additionalName ?nameNewborn .
               ?nameNewborn pnv:givenName ?givenNameNewborn ;
                            pnv:baseSurname ?familyNameNewborn .
    }
    OPTIONAL { ?newbornID sdo:parent ?fatherNewbornID .
               ?fatherNewbornID sdo:gender ?genderNewbornFather .
               FILTER(?genderNewbornFather = sdo:Male)
               ?fatherNewbornID sdo:identifier ?idNewbornFather ;
                                sdo:additionalName ?nameNewbornFather .
               ?nameNewbornFather pnv:givenName ?givenNameNewbornFather ;
                                  pnv:baseSurname ?familyNameNewbornFather .
               OPTIONAL { FILTER(BOUND(?fatherNewbornID)) ?fatherNewbornID picom:hasAge ?ageNewbornFather }
    }
    OPTIONAL { ?newbornID sdo:parent ?motherNewbornID .
               ?motherNewbornID sdo:gender ?genderNewbornMother .
               FILTER(?genderNewbornMother = sdo:Female)
               ?motherNewbornID sdo:identifier ?idNewbornMother ;
                                sdo:additionalName ?nameNewbornMother .
               ?nameNewbornMother pnv:givenName ?givenNameNewbornMother ;
                                  pnv:baseSurname ?familyNameNewbornMother .
               OPTIONAL { FILTER(BOUND(?motherNewbornID)) ?motherNewbornID picom:hasAge ?ageNewbornMother }
    }
  }

# Return all information pertaining to death
# events, including information about the
# deceased, its partner, and its parents.
DEATHS: >
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    ?event sdo:additionalType picot:sourcetypes\/553 ;
           sdo:dateCreated ?eventDate ;
           sdo:identifier ?eventID .
    ?deceasedID prov:hadPrimarySource ?event ;
                picom:hasRole picot:roles\/479 ;
                sdo:identifier ?idDeceased .
    OPTIONAL { ?deceasedID sdo:gender ?genderDeceased }
    OPTIONAL { ?deceasedID picom:hasAge ?ageDeceased }
    OPTIONAL { ?deceasedID sdo:additionalName ?nameDeceased .
               ?nameDeceased pnv:givenName ?givenNameDeceased ;
                             pnv:baseSurname ?familyNameDeceased .
    }
    OPTIONAL { ?deceasedID sdo:parent ?fatherDeceasedID .
               ?fatherDeceasedID sdo:gender ?genderDeceasedFather .
               FILTER(?genderDeceasedFather = sdo:Male)
               ?fatherDeceasedID sdo:identifier ?idDeceasedFather ;
                                 sdo:additionalName ?nameDeceasedFather .
               ?nameDeceasedFather pnv:givenName ?givenNameDeceasedFather ;
                                   pnv:baseSurname ?familyNameDeceasedFather .
               OPTIONAL { FILTER(BOUND(?fatherDeceasedID)) ?fatherDeceasedID picom:hasAge ?ageDeceasedFather }
    }
    OPTIONAL { ?deceasedID sdo:parent ?motherDeceasedID .
               ?motherDeceasedID sdo:gender ?genderDeceasedMother .
               FILTER(?genderDeceasedMother = sdo:Female)
               ?motherDeceasedID sdo:identifier ?idDeceasedMother ;
                                 sdo:additionalName ?nameDeceasedMother .
               ?nameDeceasedMother pnv:givenName ?givenNameDeceasedMother ;
                                   pnv:baseSurname ?familyNameDeceasedMother .
               OPTIONAL { FILTER(BOUND(?motherDeceasedID)) ?motherDeceasedID picom:hasAge ?ageDeceasedMother }
    }
    OPTIONAL { ?deceasedID sdo:spouse ?partnerID .
               ?partnerID sdo:identifier ?idPartner ;
                          sdo:additionalName ?namePartner .
               ?namePartner pnv:givenName ?givenNamePartner ;
                            pnv:baseSurname ?familyNamePartner .
               OPTIONAL { FILTER(BOUND(?partnerID)) ?partnerID sdo:gender ?genderPartner }
    }
  }

# Return all information pertaining to marriage
# events, including information about the
# bride and groom, and of both their parents.
MARRIAGES: >  
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    ?event sdo:additionalType picot:sourcetypes\/552 ;
           sdo:dateCreated ?eventDate ;
           sdo:identifier ?eventID .

    ?brideID prov:hadPrimarySource ?event ;
             picom:hasRole picot:roles\/574 ;
             sdo:gender ?genderBride .
             FILTER(?genderBride = sdo:Female)
    ?brideID sdo:identifier ?idBride ;
             sdo:additionalName ?nameBride .
    ?nameBride pnv:givenName ?givenNameBride ;
               pnv:baseSurname ?familyNameBride .
    OPTIONAL { ?brideID picom:hasAge ?ageBride }

    ?brideID sdo:spouse ?groomID .
    ?groomID prov:hadPrimarySource ?event ;
             picom:hasRole picot:roles\/574 ;
             sdo:gender ?genderGroom .
    FILTER(?genderGroom = sdo:Male)
    ?groomID sdo:identifier ?idGroom ;
             sdo:additionalName ?nameGroom .
    ?nameGroom pnv:givenName ?givenNameGroom ;
               pnv:baseSurname ?familyNameGroom .
    OPTIONAL { ?groomID picom:hasAge ?ageGroom }

    OPTIONAL { ?brideID sdo:parent ?fatherBrideID .
               ?fatherBrideID prov:hadPrimarySource ?event ;
                              sdo:gender ?genderBrideFather .
               FILTER(?genderBrideFather = sdo:Male)
               ?fatherBrideID sdo:identifier ?idBrideFather ;
                              sdo:additionalName ?nameBrideFather .
               ?nameBrideFather pnv:givenName ?givenNameBrideFather ;
                                pnv:baseSurname ?familyNameBrideFather .
               OPTIONAL { FILTER(BOUND(?fatherBrideID)) ?fatherBrideID picom:hasAge ?ageBrideFather }
    }
    OPTIONAL { ?brideID sdo:parent ?motherBrideID .
               ?motherBrideID prov:hadPrimarySource ?event ;
                              sdo:gender ?genderBrideMother .
               FILTER(?genderBrideMother = sdo:Female)
               ?motherBrideID sdo:identifier ?idBrideMother ;
                              sdo:additionalName ?nameBrideMother .
               ?nameBrideMother pnv:givenName ?givenNameBrideMother ;
                                pnv:baseSurname ?familyNameBrideMother .
               OPTIONAL { FILTER(BOUND(?motherBrideID)) ?motherBrideID picom:hasAge ?ageBrideMother }
    }
    OPTIONAL { ?groomID sdo:parent ?fatherGroomID .
               ?fatherGroomID prov:hadPrimarySource ?event ;
                              sdo:gender ?genderGroomFather .
               FILTER(?genderGroomFather = sdo:Male)
               ?fatherGroomID sdo:identifier ?idGroomFather ;
                              sdo:additionalName ?nameGroomFather .
               ?nameGroomFather pnv:givenName ?givenNameGroomFather ;
                                pnv:baseSurname ?familyNameGroomFather .
               OPTIONAL { FILTER(BOUND(?fatherGroomID)) ?fatherGroomID picom:hasAge ?ageGroomFather }
    }
    OPTIONAL { ?groomID sdo:parent ?motherGroomID .
               ?motherGroomID prov:hadPrimarySource ?event ;
                              sdo:gender ?genderGroomMother .
               FILTER(?genderGroomMother = sdo:Female)
               ?motherGroomID sdo:identifier ?idGroomMother ;
                              sdo:additionalName ?nameGroomMother .
               ?nameGroomMother pnv:givenName ?givenNameGroomMother ;
                                pnv:baseSurname ?familyNameGroomMother .
               OPTIONAL { FILTER(BOUND(?motherGroomID)) ?motherGroomID picom:hasAge ?ageGroomMother }
    }
  }

...  # EOF
