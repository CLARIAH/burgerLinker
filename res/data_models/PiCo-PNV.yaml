---
# ********************************************* #
#                                               #
#  -------------------------------------------  #
#  CBG BurgerLinker Persons in Context Model    #
#  -------------------------------------------  #
#                                               #
#  This file specifies the data model used by   #
#  the input data to BurgerLinker. All classes  #
#  and properties in this file must be defined. #
#                                               #
#  The data model specified in this files is    #
#  based upon the Persons in Context schema [1] #
#  with person names modelled using PNV.        #
#                                               #
#  [1] https://personsincontext.org             #
#                                               #
#  Version: NOV 2025                            #
#                                               #
# ********************************************* #


# ============================================= #
#                                               #
# SPARQL Queries                                #
#                                               #
# ============================================= #

# Return all information pertaining to birth
# events, including information about the newborn
# and its parents.
BIRTHS: >
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    {
      SELECT DISTINCT *
      WHERE {
        ?event sdo:additionalType picot:sourcetypes\/551 ;
               sdo:dateCreated ?eventDate .
        ?newborn prov:hadPrimarySource ?event ;
                 picom:hasRole picot:roles\/575 .
        }
    }
    OPTIONAL { ?newborn sdo:gender ?newbornGender }
    OPTIONAL { ?newborn picom:hasAge ?newbornAge }
    OPTIONAL { ?newborn sdo:additionalName ?newbornName .
               ?newbornName pnv:givenName ?newbornGivenName ;
                            pnv:baseSurname ?newbornFamilyName .
    }
    OPTIONAL { ?newborn sdo:parent ?newbornFather .
               ?newbornFather sdo:gender ?newbornFatherGender .
               FILTER(?newbornFatherGender = sdo:Male)
               ?newbornFather sdo:additionalName ?newbornFatherName .
               ?newbornFatherName pnv:givenName ?newbornFatherGivenName ;
                                  pnv:baseSurname ?newbornFatherFamilyName .
               OPTIONAL { FILTER(BOUND(?newbornFather)) ?newbornFather picom:hasAge ?newbornFatherAge }
    }
    OPTIONAL { ?newborn sdo:parent ?newbornMother .
               ?newbornMother sdo:gender ?newbornMotherGender .
               FILTER(?newbornMotherGender = sdo:Female)
               ?newbornMother sdo:additionalName ?newbornMotherName .
               ?newbornMotherName pnv:givenName ?newbornMotherGivenName ;
                                  pnv:baseSurname ?newbornMotherFamilyName .
               OPTIONAL { FILTER(BOUND(?newbornMother)) ?newbornMother picom:hasAge ?newbornMotherAge }
    }
  }

# Return all information pertaining to death
# events, including information about the
# deceased, its partner, and its parents.
DEATHS: >
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    {
      SELECT DISTINCT *
      WHERE {
      ?event sdo:additionalType picot:sourcetypes\/553 ;
             sdo:dateCreated ?eventDate .
      ?deceased prov:hadPrimarySource ?event ;
                picom:hasRole picot:roles\/479 .
      }
    }
    OPTIONAL { ?deceased sdo:gender ?deceasedGender }
    OPTIONAL { ?deceased picom:hasAge ?deceasedAge }
    OPTIONAL { ?deceased sdo:additionalName ?deceasedName .
               ?deceasedName pnv:givenName ?deceasedGivenName ;
                             pnv:baseSurname ?deceasedFamilyName .
    }
    OPTIONAL { ?deceased sdo:parent ?deceasedFather .
               ?deceasedFather sdo:gender ?deceasedFatherGender .
               FILTER(?deceasedFatherGender = sdo:Male)
               ?deceasedFather sdo:additionalName ?deceasedFatherName .
               ?deceasedFatherName pnv:givenName ?deceasedFatherGivenName ;
                                   pnv:baseSurname ?deceasedFatherFamilyName .
               OPTIONAL { FILTER(BOUND(?deceasedFather)) ?deceasedFather picom:hasAge ?deceasedFatherAge }
    }
    OPTIONAL { ?deceased sdo:parent ?deceasedMother .
               ?deceasedMother sdo:gender ?deceasedMotherGender .
               FILTER(?deceasedMotherGender = sdo:Female)
               ?deceasedMother sdo:additionalName ?deceasedMotherName .
               ?deceasedMotherName pnv:givenName ?deceasedMotherGivenName ;
                                   pnv:baseSurname ?deceasedMotherFamilyName .
               OPTIONAL { FILTER(BOUND(?deceasedMother)) ?deceasedMother picom:hasAge ?deceasedMotherAge }
    }
    OPTIONAL { ?deceased sdo:spouse ?partner .
               ?partner sdo:additionalName ?partnerName .
               ?partnerName pnv:givenName ?partnerGivenName ;
                            pnv:baseSurname ?partnerFamilyName .
               OPTIONAL { FILTER(BOUND(?partner)) ?partner sdo:gender ?partnerGender }
    }
  }

MARRIAGES: >  
  PREFIX picom: <https://personsincontext.org/model#>
  PREFIX picot: <https://terms.personsincontext.org/>
  PREFIX pnv: <https://w3id.org/pnv#>
  PREFIX prov: <http://www.w3.org/ns/prov#>
  PREFIX sdo: <https://schema.org/>

  SELECT DISTINCT *
  WHERE {
    { 
      SELECT DISTINCT *
      WHERE {
        ?event sdo:additionalType picot:sourcetypes\/552 ;
               sdo:dateCreated ?eventDate .
       
        ?bride prov:hadPrimarySource ?event ;
               picom:hasRole picot:roles\/574 ;
               sdo:gender ?brideGender .
        FILTER(?brideGender = sdo:Female)
        ?bride sdo:additionalName ?brideName .
        ?brideName pnv:givenName ?brideGivenName ;
                   pnv:baseSurname ?brideFamilyName .

        ?bride sdo:spouse ?groom .
        ?groom prov:hadPrimarySource ?event ;
               picom:hasRole picot:roles\/574 ;
               sdo:gender ?groomGender .
        FILTER(?groomGender = sdo:Male)
        ?groom sdo:additionalName ?groomName .
        ?groomName pnv:givenName ?groomGivenName ;
                   pnv:baseSurname ?groomFamilyName .
        }
      }
    OPTIONAL { FILTER(BOUND(?bride)) ?bride picom:hasAge ?brideAge }
    OPTIONAL { FILTER(BOUND(?groom)) ?groom picom:hasAge ?groomAge }
    OPTIONAL { ?bride sdo:parent ?brideFather .
               ?brideFather prov:hadPrimarySource ?event ;
                            sdo:gender ?brideFatherGender .
               FILTER(?brideFatherGender = sdo:Male)
               ?brideFather sdo:additionalName ?brideFatherName .
               ?brideFatherName pnv:givenName ?brideFatherGivenName ;
                                pnv:baseSurname ?brideFatherFamilyName .
               OPTIONAL { FILTER(BOUND(?brideFather)) ?brideFather picom:hasAge ?brideFatherAge }
    }
    OPTIONAL { ?bride sdo:parent ?brideMother .
               ?brideMother prov:hadPrimarySource ?event ;
                            sdo:gender ?brideMotherGender .
               FILTER(?brideMotherGender = sdo:Female)
               ?brideMother sdo:additionalName ?brideMotherName .
               ?brideMotherName pnv:givenName ?brideMotherGivenName ;
                                pnv:baseSurname ?brideMotherFamilyName .
               OPTIONAL { FILTER(BOUND(?brideMother)) ?brideMother picom:hasAge ?brideMotherAge }
    }
    OPTIONAL { ?groom sdo:parent ?groomFather .
               ?groomFather prov:hadPrimarySource ?event ;
                            sdo:gender ?groomFatherGender .
               FILTER(?groomFatherGender = sdo:Male)
               ?groomFather sdo:additionalName ?groomFatherName .
               ?groomFatherName pnv:givenName ?groomFatherGivenName ;
                                pnv:baseSurname ?groomFatherFamilyName .
               OPTIONAL { FILTER(BOUND(?groomFather)) ?groomFather picom:hasAge ?groomFatherAge }
    }
    OPTIONAL { ?groom sdo:parent ?groomMother .
               ?groomMother prov:hadPrimarySource ?event ;
                            sdo:gender ?groomMotherGender .
               FILTER(?groomMotherGender = sdo:Female)
               ?groomMother sdo:additionalName ?groomMotherName .
               ?groomMotherName pnv:givenName ?groomMotherGivenName ;
                                pnv:baseSurname ?groomMotherFamilyName .
               OPTIONAL { FILTER(BOUND(?groomMother)) ?groomMother picom:hasAge ?groomMotherAge }
    }
  }
...  # EOF
