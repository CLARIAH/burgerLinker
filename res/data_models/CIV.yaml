---
# ********************************************* #
#                                               #
#  -------------------------------------------  #
#  KNAW BurgerLinker Civil Registry Data Model  #
#  -------------------------------------------  #
#                                               #
#  This file specifies the data model used by   #
#  the input data to BurgerLinker. All          #
#  requested variable bindings must be          #
#  resolvable for BurgerLinker to work          #
#  properly.                                    #
#                                               #
#  The data model specified in this files is    #
#  based upon the Civil Registry Schema, and is #
#  backwards compatible with past versions of   #
#  BurgerLinker.                                #
#                                               #
#  Version: AUG 2025                            #
#                                               #
# ********************************************* #


# ============================================= #
#                                               #
# SPARQL Queries                                #
#                                               #
# ============================================= #

# Return all information pertaining to birth
# events, including information about the newborn
# and its parents.
BIRTHS: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:newborn ?newbornID ;
         civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  ?newbornID civ:personID ?idNewborn .
  OPTIONAL { ?newbornID sdo:givenName ?givenNameNewborn }
  OPTIONAL { ?newbornID sdo:familyName ?familyNameNewborn }
  OPTIONAL { ?newbornID sdo:gender ?genderNewborn }
  OPTIONAL { ?newbornID civ:age ?ageNewborn }
  OPTIONAL { ?event civ:father ?fatherNewbornID .
             ?fatherNewbornID civ:personID ?idNewbornFather ;
                              sdo:givenName ?givenNameNewbornFather ;
                              sdo:familyName ?familyNameNewbornFather .
             OPTIONAL { FILTER(BOUND(?fatherNewbornID)) ?fatherNewbornID sdo:gender ?genderNewbornFather }
             OPTIONAL { FILTER(BOUND(?fatherNewbornID)) ?fatherNewbornID civ:age ?ageNewbornFather }
           }
  OPTIONAL { ?event civ:mother ?motherNewbornID .
             ?motherNewbornID civ:personID ?idNewbornMother ;
                              sdo:givenName ?givenNameNewbornMother ;
                              sdo:familyName ?familyNameNewbornMother .
             OPTIONAL { FILTER(BOUND(?motherNewbornID)) ?motherNewbornID sdo:gender ?genderNewbornMother }
             OPTIONAL { FILTER(BOUND(?motherNewbornID)) ?motherNewbornID civ:age ?ageNewbornMother }
           }
  }

# Return all information pertaining to death
# events, including information about the
# deceased, its partner, and its parents.
DEATHS: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:deceased ?deceasedID ;
         civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  ?deceasedID civ:personID ?idDeceased .
  OPTIONAL { ?deceasedID sdo:givenName ?givenNameDeceased }
  OPTIONAL { ?deceasedID sdo:familyName ?familyNameDeceased }
  OPTIONAL { ?deceasedID sdo:gender ?genderDeceased }
  OPTIONAL { ?deceasedID civ:age ?ageDeceased }
  OPTIONAL { ?event civ:partner ?partnerID .
             ?partnerID civ:personID ?idPartner ;
                        sdo:givenName ?givenNamePartner ;
                        sdo:familyName ?familyNamePartner .
             OPTIONAL { FILTER(BOUND(?partnerID)) ?partnerID sdo:gender ?genderPartner }
           }
  OPTIONAL { ?event civ:father ?fatherDeceasedID .
             ?fatherDeceasedID civ:personID ?idDeceasedFather ;
                               sdo:givenName ?givenNameDeceasedFather ;
                               sdo:familyName ?familyNameDeceasedFather .
             OPTIONAL { FILTER(BOUND(?fatherDeceasedID)) ?fatherDeceasedID sdo:gender ?genderDeceasedFather }
             OPTIONAL { FILTER(BOUND(?fatherDeceasedID)) ?fatherDeceasedID civ:age ?ageDeceasedFather }
           }
  OPTIONAL { ?event civ:mother ?motherDeceasedID .
             ?motherDeceasedID civ:personID ?idDeceasedMother ;
                               sdo:givenName ?givenNameDeceasedMother ;
                               sdo:familyName ?familyNameDeceasedMother .
             OPTIONAL { FILTER(BOUND(?motherDeceasedID)) ?motherDeceasedID sdo:gender ?genderDeceasedMother }
             OPTIONAL { FILTER(BOUND(?motherDeceasedID)) ?motherDeceasedID civ:age ?ageDeceasedMother }
           }
  }

# Return all information pertaining to marriage
# events, including information about the
# bride and groom, and of both their parents.
MARRIAGES: >
  PREFIX civ: <https://iisg.amsterdam/id/civ/>
  PREFIX sdo: <https://schema.org/>

  SELECT *
  WHERE {
  ?event civ:eventDate ?eventDate ;
         civ:registrationID ?eventID .
  OPTIONAL { ?event civ:bride ?brideID .
             ?brideID civ:personID ?idBride ;
                      sdo:givenName ?givenNameBride ;
                      sdo:familyName ?familyNameBride .
             OPTIONAL { FILTER(BOUND(?brideID)) ?brideID sdo:gender ?genderBride }
             OPTIONAL { FILTER(BOUND(?brideID)) ?brideID civ:age ?ageBride }
           }
  OPTIONAL { ?event civ:groom ?groomID .
             ?groomID civ:personID ?idGroom ;
                      sdo:givenName ?givenNameGroom ;
                      sdo:familyName ?familyNameGroom .
             OPTIONAL { FILTER(BOUND(?groomID)) ?groomID sdo:gender ?genderGroom }
             OPTIONAL { FILTER(BOUND(?groomID)) ?groomID civ:age ?ageGroom }
           }
  OPTIONAL { ?event civ:fatherBride ?fatherBrideID .
             ?fatherBrideID civ:personID ?idBrideFather ;
                            sdo:givenName ?givenNameBrideFather ;
                            sdo:familyName ?familyNameBrideFather .
             OPTIONAL { FILTER(BOUND(?fatherBrideID)) ?fatherBrideID sdo:gender ?genderBrideFather }
             OPTIONAL { FILTER(BOUND(?fatherBrideID)) ?fatherBrideID civ:age ?ageBrideFather }
           }
  OPTIONAL { ?event civ:motherBride ?motherBrideID .
             ?motherBrideID civ:personID ?idBrideMother ;
                            sdo:givenName ?givenNameBrideMother ;
                            sdo:familyName ?familyNameBrideMother .
             OPTIONAL { FILTER(BOUND(?motherBrideID)) ?motherBrideID sdo:gender ?genderBrideMother }
             OPTIONAL { FILTER(BOUND(?motherBrideID)) ?motherBrideID civ:age ?ageBrideMother }
           }
  OPTIONAL { ?event civ:fatherGroom ?fatherGroomID .
             ?fatherGroomID civ:personID ?idGroomFather ;
                            sdo:givenName ?givenNameGroomFather ;
                            sdo:familyName ?familyNameGroomFather .
             OPTIONAL { FILTER(BOUND(?fatherGroomID)) ?fatherGroomID sdo:gender ?genderGroomFather }
             OPTIONAL { FILTER(BOUND(?fatherGroomID)) ?fatherGroomID civ:age ?ageGroomFather }
           }
  OPTIONAL { ?event civ:motherGroom ?motherGroomID .
             ?motherGroomID civ:personID ?idGroomMother ;
                            sdo:givenName ?givenNameGroomMother ;
                            sdo:familyName ?familyNameGroomMother .
             OPTIONAL { FILTER(BOUND(?motherGroomID)) ?motherGroomID sdo:gender ?genderGroomMother }
             OPTIONAL { FILTER(BOUND(?motherGroomID)) ?motherGroomID civ:age ?ageGroomMother }
           }
  }

...  # EOF
